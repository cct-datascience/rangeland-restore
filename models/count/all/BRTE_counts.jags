model{
  for(i in 1:N){
    # Likelihood
    counts[i] ~ dpois(mu[i])
    # replicated data
    counts.rep[i] ~ dpois(mu[i])
    
    # Mean model of zero-inflated Poisson
    mu[i] <- theta[i]*area[i]*p[i] + 0.00001 
    
    # Regression model for theta (proportion)
    theta[i] <- exp(alpha + beta[1]*spring[i] + beta[2]*fall[i] + beta[3]*herbicide[i] + beta[4]*greenstrip[i])
    
    p[i] ~ dbern(psi)
    
    # Part of Dsum calculation
    Sqdiff[i] <- pow(counts.rep[i] - counts[i], 2)
  }
  
  # Relatively non-informative priors
  # Intercept
  alpha ~ dnorm(0, 0.001)
  
  # Coefficients
  for(l in 1:nL){ # grazing treatments
    beta[l] ~ dnorm(0, 0.001)
  }
  
  # Probability of BRTE presence
  psi ~ dbeta(2, 2) # proportion of non-zero values
  
  
  # Derived quantities
  # Probability of BRTE frequency if present
  prob[1] <- exp(alpha) # ungrazed, control
  prob[2] <- exp(alpha + beta[1]) # fall grazing, control
  prob[3] <- exp(alpha + beta[2]) # spring grazing, control
  prob[4] <- exp(alpha + beta[3]) # ungrazed, herbicide
  prob[5] <- exp(alpha + beta[4]) # ungrazed, greenstrip
  prob[6] <- exp(alpha + beta[1] + beta[3]) # fall grazing, herbicide
  prob[7] <- exp(alpha + beta[1] + beta[4]) # fall grazing, greenstrip
  prob[8] <- exp(alpha + beta[2] + beta[3]) # spring grazing, herbicide
  prob[9] <- exp(alpha + beta[2] + beta[4]) # spring grazing, greenstrip
  
  # Differences in BRTE frequency if present
  diff[1] <- exp(alpha + beta[1]) - exp(alpha) # effect of fall grazing
  diff[2] <- exp(alpha + beta[2]) - exp(alpha) # effect of spring grazing
  diff[3] <- exp(alpha + beta[3]) - exp(alpha) # effect of herbicide
  diff[4] <- exp(alpha + beta[4]) - exp(alpha) # effect of greenstrip

  
  # Dsum: posterior predictive loss
  Dsum <- sum(Sqdiff)
}