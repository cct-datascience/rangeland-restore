# Mixture model of Bernoulli and Poisson
# ANOVA model to predict theta, 2-way interactions included
# theta has random effects only for block (paddock within block is confounded with grazing + block)

model{
  for(i in 1:N){
    # Likelihood
    counts[i] ~ dpois(mu[i])
    p[i] ~ dbern(psi)
    
    # replicated data
    counts.rep[i] ~ dpois(mu[i])
    
    # Mean model of Poisson
    mu[i] <- theta[i]*area[i]*p[i] + 0.00001 
    
    # Regression model for theta (proportion) with random effects
    theta[i] <- exp(alpha + beta[1]*mix[i] + beta[2]*high[i] + beta[3]*coated[i] + 
    beta[4]*fall[i] + beta[5]*spring[i] + beta[6]*mix_high[i] + beta[7]*mix_coated[i] +
    beta[8]*mix_fall[i] + beta[9]*mix_spring[i] + beta[10]*high_coated[i] + 
    beta[11]*high_fall[i] + beta[12]*high_spring[i] + beta[13]*coated_fall[i] +
    beta[14]*coated_spring[i] +
    eps[block[i]]) 

    # Part of Dsum calculation
    Sqdiff[i] <- pow(counts.rep[i] - counts[i], 2)
  }
  
  # Identifiable intercept, post sweeping random effect means into intercept
  alpha.star <- alpha + mean(eps[])
  
  # Relatively non-informative priors
  
  # random effects of block
  for(b in 1:Nb){ # number of blocks
    eps[b] ~ dnorm(0, tau.eps)
    
    # Identifiable random effects - post-sweeping of mean REs by block
    eps.star[b] <- eps[b] - mean(eps[])
  }
  
  
  # Block level precisions with folded T priors
  tau.Eps ~ dt(0, Bb, 2)
  sig.eps <- abs(tau.Eps)
  tau.eps <- pow(sig.eps, -2)
  
  # dt parameters (set as data)
  Bb <- 1/(Ab*Ab)
  
  # Coefficients
  alpha ~ dnorm(0, 0.0001)
  
  for(l in 1:nL){ # Number of treatment offsets
    beta[l] ~ dnorm(0, 0.0001)
  }
  
  # Probability of BRTE presence
  psi ~ dbeta(2, 2) # relatively non-informative for proportion of non-zero values
  
  
  # Derived quantities
  # Probability of BRTE frequency if present (2x2x2 = 8 combinations of 3 factors with 2 levels)
  prob[1] <- exp(alpha.star) # mono, low, uncoated
  prob[2] <- exp(alpha.star + beta[1]) # mix, low, uncoated
  prob[3] <- exp(alpha.star + beta[2]) # mono, high, uncoated
  prob[4] <- exp(alpha.star + beta[3]) # mono, low, coated
  prob[5] <- exp(alpha.star + beta[1] + beta[2] + beta[4]) # mix, high, uncoated
  prob[6] <- exp(alpha.star + beta[1] + beta[3] + beta[5]) # mix, low, coated
  prob[7] <- exp(alpha.star + beta[2] + beta[3] + beta[6]) # mono, high, coated
  prob[8] <- exp(alpha.star + beta[1] + beta[2] + beta[3] + beta[4] + beta[5] + beta[6]) # mix, high, coated
  

  # Differences in BRTE frequency if present
  diff[1] <- exp(alpha.star + beta[1]) - exp(alpha.star) # main effect of mixture
  diff[2] <- exp(alpha.star + beta[2]) - exp(alpha.star) # main effect of high seed rate
  diff[3] <- exp(alpha.star + beta[3]) - exp(alpha.star) # main effect of coated seeds
  diff[4] <- exp(alpha.star + beta[1] + beta[2] + beta[4]) - exp(alpha.star) # interaction effect of mixture and high seed rate
  diff[5] <- exp(alpha.star + beta[1] + beta[3] + beta[5]) - exp(alpha.star) # interaction effect of mixture and coated seeds
  diff[6] <- exp(alpha.star + beta[2] + beta[3] + beta[6]) - exp(alpha.star) # interaction effect of high seed rate and coated seeds
  
  
  # Dsum: posterior predictive loss
  Dsum <- sum(Sqdiff)
}