# Poisson only model
# ANOVA model to predict theta, 2-way interactions included
# theta has random effects only for block (paddock within block is confounded with grazing + block)

model{
  for(i in 1:N){
    # Likelihood
    counts[i] ~ dpois(mu[i])
    p[i] ~ dbern(psi)

    # replicated data
    counts.rep[i] ~ dpois(mu[i])
    
    # Mean model of Poisson
    mu[i] <- theta[i]*area[i]*p[i] + 0.00001
    
    # Regression model for theta (proportion) with random effects
    theta[i] <- exp(alpha + beta[1]*mix[i] + beta[2]*high[i] + beta[3]*coated[i] + 
    beta[4]*fall[i] + beta[5]*spring[i] + beta[6]*mix_high[i] + beta[7]*mix_coated[i] +
    beta[8]*mix_fall[i] + beta[9]*mix_spring[i] + beta[10]*high_coated[i] + 
    beta[11]*high_fall[i] + beta[12]*high_spring[i] + beta[13]*coated_fall[i] +
    beta[14]*coated_spring[i] +
    eps[block[i]]) 

    # Part of Dsum calculation
    Sqdiff[i] <- pow(counts.rep[i] - counts[i], 2)
  }
  
 # Identifiable intercept, post sweeping random effect means into intercept
  alpha.star <- alpha + mean(eps[])
  
  # Relatively non-informative priors
  
  # random effects of block
  for(b in 1:Nb){ # number of blocks
    eps[b] ~ dnorm(0, tau.eps)
    
    # Identifiable random effects - post-sweeping of mean REs by block
    eps.star[b] <- eps[b] - mean(eps[])
  }
  
  # Block level precisions with folded T priors
  tau.Eps ~ dt(0, Bb, 2)
  sig.eps <- abs(tau.Eps)
  tau.eps <- pow(sig.eps, -2)
  
  # dt parameters (set as data)
  Bb <- 1/(Ab*Ab)
  
  # Coefficients
  alpha ~ dnorm(0, 0.0001)
  
  for(l in 1:nL){ # Number of treatment offsets
    beta[l] ~ dnorm(0, 0.0001)
  }
    
  # Probability of BRTE presence
  psi ~ dunif(0, 1)
  
    # Calculations to monitor, only two-way combinations
  int_Beta[1] <- beta[1] + beta[2] + beta[6] # mix:high
  int_Beta[2] <- beta[1] + beta[3] + beta[7] # mix:coated
  int_Beta[3] <- beta[1] + beta[4] + beta[8] # mix:fall
  int_Beta[4] <- beta[1] + beta[5] + beta[9] # mix:spring
  int_Beta[5] <- beta[2] + beta[3] + beta[10] # high:coated
  int_Beta[6] <- beta[2] + beta[4] + beta[11] # high:fall
  int_Beta[7] <- beta[2] + beta[5] + beta[12] # high:spring
  int_Beta[8] <- beta[3] + beta[4] + beta[13] # coated:fall
  int_Beta[9] <- beta[3] + beta[5] + beta[14] # coated:spring

  # Calculations to monitor, only two-way combinations
  Diff_Beta[1] <- exp(alpha.star + beta[1]) - exp(alpha.star) # mix
  Diff_Beta[2] <- exp(alpha.star + beta[2]) - exp(alpha.star) # high
  Diff_Beta[3] <- exp(alpha.star + beta[3]) - exp(alpha.star) # coated
  Diff_Beta[4] <- exp(alpha.star + beta[4]) - exp(alpha.star) # fall
  Diff_Beta[5] <- exp(alpha.star + beta[5]) - exp(alpha.star) # spring

  
diff_Beta[1] <- exp(alpha.star + beta[1] + beta[2] + beta[6]) - exp(alpha.star) # mix:high
diff_Beta[2] <- exp(alpha.star + beta[1] + beta[3] + beta[7]) - exp(alpha.star) # mix:coated
diff_Beta[3] <- exp(alpha.star + beta[1] + beta[4] + beta[8]) - exp(alpha.star) # mix:fall
diff_Beta[4] <- exp(alpha.star + beta[1] + beta[5] + beta[9]) - exp(alpha.star) # mix:spring
diff_Beta[5] <- exp(alpha.star + beta[2] + beta[3] + beta[10]) - exp(alpha.star) # high:coated
diff_Beta[6] <- exp(alpha.star + beta[2] + beta[4] + beta[11]) - exp(alpha.star) # high:fall
diff_Beta[7] <- exp(alpha.star + beta[2] + beta[5] + beta[12]) - exp(alpha.star) # high:spring
diff_Beta[8] <- exp(alpha.star + beta[3] + beta[4] + beta[13]) - exp(alpha.star) # coated:fall
diff_Beta[9] <- exp(alpha.star + beta[3] + beta[5] + beta[14]) - exp(alpha.star) # coated:spring  
  
  
  # Monitor the mean of each group
  m.mono.low.uncoated.ungrazed <- exp(alpha.star) 
  m.mono.low.uncoated.fall <- exp(alpha.star + beta[4]) 
  m.mono.low.uncoated.spring <- exp(alpha.star + beta[5]) 
  
  m.mono.low.coated.ungrazed <- exp(alpha.star + beta[3]) 
  m.mono.low.coated.fall <- exp(alpha.star + beta[3] + beta[4] + beta[13]) 
  m.mono.low.coated.spring <- exp(alpha.star + beta[3] + beta[5] + beta[14]) 
  
  m.mono.high.uncoated.ungrazed <- exp(alpha.star + beta[2]) 
  m.mono.high.uncoated.fall <- exp(alpha.star + beta[4] + beta[2] + beta[11]) 
  m.mono.high.uncoated.spring <- exp(alpha.star + beta[5] + beta[2] + beta[12]) 
  
  m.mono.high.coated.ungrazed <- exp(alpha.star + beta[3] + beta[2] + beta[10]) 
  m.mono.high.coated.fall <- exp(alpha.star + beta[3] + beta[4] + beta[13] + beta[2] + beta[11] + beta[10]) 
  m.mono.high.coated.spring <- exp(alpha.star + beta[3] + beta[5] + beta[14] + beta[2] + beta[12] + beta[10])
  
  m.mix.low.uncoated.ungrazed <- exp(alpha.star + beta[1]) 
  m.mix.low.uncoated.fall <- exp(alpha.star + beta[4] + beta[1] + beta[8]) 
  m.mix.low.uncoated.spring <- exp(alpha.star + beta[5] + beta[1] + beta[9]) 
  
  m.mix.low.coated.ungrazed <- exp(alpha.star + beta[3] + beta[1] + beta[7]) 
  m.mix.low.coated.fall <- exp(alpha.star + beta[3] + beta[4] + beta[13] + beta[1] + beta[8] + beta[7]) 
  m.mix.low.coated.spring <- exp(alpha.star + beta[3] + beta[5] + beta[14] + beta[1] + beta[9] + beta[7]) 
  
  m.mix.high.uncoated.ungrazed <- exp(alpha.star + beta[2] + beta[1] + beta[6]) 
  m.mix.high.uncoated.fall <- exp(alpha.star + beta[4] + beta[2] + beta[11] + beta[1] + beta[6] + beta[8]) 
  m.mix.high.uncoated.spring <- exp(alpha.star + beta[5] + beta[2] + beta[12] + beta[1] + beta[6] + beta[9]) 
  
  m.mix.high.coated.ungrazed <- exp(alpha.star + beta[3] + beta[2] + beta[10] + beta[1] + beta[6] + beta[7]) 
  m.mix.high.coated.fall <- exp(alpha.star + beta[3] + beta[4] + beta[13] + beta[2] + beta[11] + beta[10] + beta[1] + beta[6] + beta[7] + beta [8]) 
  m.mix.high.coated.spring <- exp(alpha.star + beta[3] + beta[5] + beta[14] + beta[2] + beta[12] + beta[10] + beta[1] + beta[6] + beta[7] + beta[9])
  
  # Dsum: posterior predictive loss
  Dsum <- sum(Sqdiff)
}