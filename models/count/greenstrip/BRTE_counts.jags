model{
  for(i in 1:N){
    # Likelihood
    counts[i] ~ dpois(mu[i])
    # replicated data
    counts.rep[i] ~ dpois(mu[i])
    
    # Mean model of zero-inflated Poisson
    mu[i] <- theta[i]*area[i]*p[i] + 0.00001 
    
    # Regression model for theta (proportion) with random effects
    theta[i] <- exp(alpha + beta[1]*fall[i] + beta[2]*spring[i] + beta[3]*mix[i] + beta[4]*high[i] + beta[5]*coated[i] + eps[pad[i]] + gam[block[pad[i]]])
    
    p[i] ~ dbern(psi)
    
    # Part of Dsum calculation
    Sqdiff[i] <- pow(counts.rep[i] - counts[i], 2)
  }
  
  # Identifiable intercept, post sweeping random effect means into intercept
  alpha.star <- alpha + mean(gam[]) + mean(eps.avg[])
  
  # Relatively non-informative priors
  
  # random effects of paddock nested within block
  for(b in 1:Nb){ # number of blocks
    gam[b] ~ dnorm(0, tau.gam)
    
    # Identifiable random effects
    gam.star[b] <- gam[b] + eps.avg[b] - gam.avg - mean.eps.avg
    
    # Compute average non-identifiable paddock random effect within each block
    eps.avg[b] <- mean(eps[(3*(b-1)+1):(3*b)])
  }
  
  # Compute average block random effect and mean of mean paddock random effects
  gam.avg <- mean(gam[])
  mean.eps.avg <- mean(eps.avg[])
  
  # Random effects of paddocks within block
  for(p in 1:Np){ # number of paddocks
    eps[p] ~ dnorm(0, tau.eps)
    
    # Identifiable random effects - post-sweeping of mean REs by block
    eps.star[p] <- eps[p] - eps.avg[block[p]] 
  }
  
  # Paddock and block level precisions with folded T priors
  tau.Eps ~ dt(0, Bb, 2)
  sig.eps <- abs(tau.Eps)
  tau.eps <- pow(sig.eps, -2)
  
  
  tau.Gam ~ dt(0, Bb, 2)
  sig.gam <- abs(tau.Gam)
  tau.gam <- pow(sig.gam, -2)
  
  # dt parameters (set as data)
  Bb <- 1/(Ab*Ab)
  
  # Intercept
  alpha ~ dnorm(0, 0.0001)
  
  # Coefficients
  for(l in 1:nL){ # Number of treatment offsets
    beta[l] ~ dnorm(0, 0.0001)
  }
  
  # Probability of BRTE presence
  psi ~ dbeta(2, 2) # Proportion of non-zero values
  
  
  # Derived quantities

  # Differences in BRTE frequency if present
  diff[1] <- exp(alpha + beta[1]) - exp(alpha) # effect of fall grazing
  diff[2] <- exp(alpha + beta[2]) - exp(alpha) # effect of spring grazing
  diff[3] <- exp(alpha + beta[3]) - exp(alpha) # effect of mixture
  diff[4] <- exp(alpha + beta[4]) - exp(alpha) # effect of high seed rate
  diff[5] <- exp(alpha + beta[5]) - exp(alpha) # effect of coated seeds
  
  
  # Dsum: posterior predictive loss
  Dsum <- sum(Sqdiff)
}