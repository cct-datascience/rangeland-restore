# Poisson-only model
# ANOVA model to predict theta, 2-way interactions included
# theta has random effects only for block (paddock within block is confounded with grazing + block)

model{
  for(i in 1:N){
    # Likelihood
    counts[i] ~ dpois(mu[i])

    # replicated data
    counts.rep[i] ~ dpois(mu[i])
    
    # Mean model of Poisson
    mu[i] <- theta[i]*area[i]
    
    # Regression model for theta (proportion) with random effects
    theta[i] <- exp(alpha + beta[1]*POSE[i] + beta[2]*POFE[i] + beta[3]*VUMI[i] +
    beta[4]*ELEL[i] + beta[5]*high[i] + beta[6]*fall[i] + beta[7]*spring[i] +
    beta[8]*coated[i] +
    beta[9]*POSE[i]*high[i] + beta[10]*POFE[i]*high[i] + beta[11]*VUMI[i]*high[i]+ beta[12]*ELEL[i]*high[i] +
    beta[13]*POSE[i]*fall[i] + beta[14]*POFE[i]*fall[i] + beta[15]*VUMI[i]*fall[i] + beta[16]*ELEL[i]*fall[i] + 
    beta[17]*POSE[i]*spring[i] + beta[18]*POFE[i]*spring[i] + beta[19]*VUMI[i]*spring[i] + beta[20]*ELEL[i]*spring[i] + 
    beta[21]*POSE[i]*coated[i] + beta[22]*POFE[i]*coated[i] + beta[23]*VUMI[i]*coated[i] + beta[24]*ELEL[i]*coated[i] + 
    beta[25]*high[i]*fall[i] + beta[26]*high[i]*spring[i] + beta[27]*high[i]*coated[i] + 
    beta[28]*fall[i]*coated[i] + beta[29]*spring[i]*coated[i] +
    eps[block[i]])

    # Part of Dsum calculation
    Sqdiff[i] <- pow(counts.rep[i] - counts[i], 2)
  }
  
  # Identifiable intercept, post sweeping random effect means into intercept
  alpha.star <- alpha + mean(eps[])
  
  # Relatively non-informative priors
  
  # random effects of block
  for(b in 1:Nb){ # number of blocks
    eps[b] ~ dnorm(0, tau.eps)
    
    # Identifiable random effects - post-sweeping of mean REs by block
    eps.star[b] <- eps[b] - mean(eps[])
  }
  
  # Block level precisions with folded T priors
  tau.Eps ~ dt(0, Bb, 2)
  sig.eps <- abs(tau.Eps)
  tau.eps <- pow(sig.eps, -2)
  
  # dt parameters (set as data)
  Bb <- 1/(Ab*Ab)
  
  # Coefficients
  alpha ~ dnorm(0, 0.0001)

  for(l in 1:nL){ # Number of treatment offsets
    beta[l] ~ dnorm(0, 0.0001)
  }
  
  # Calculations to monitor, only two-way combinations
  int_Beta[1] <- beta[1] + beta[5] + beta[9] # POSE:high
  int_Beta[2] <- beta[2] + beta[5] + beta[10] # POFE:high
  int_Beta[3] <- beta[3] + beta[5] + beta[11] # VUMI:high
  int_Beta[4] <- beta[4] + beta[5] + beta[12] # ELEL:high
  int_Beta[5] <- beta[1] + beta[6] + beta[13] # POSE:fall
  int_Beta[6] <- beta[2] + beta[6] + beta[14] # POFE:fall
  int_Beta[7] <- beta[3] + beta[6] + beta[15] # VUMI:fall
  int_Beta[8] <- beta[4] + beta[6] + beta[16] # ELEL:fall
  int_Beta[9] <- beta[1] + beta[7] + beta[17] # POSE:spring
  int_Beta[10] <- beta[2] + beta[7] + beta[18] # POFE:spring
  int_Beta[11] <- beta[3] + beta[7] + beta[19] # VUMI:spring
  int_Beta[12] <- beta[4] + beta[7] + beta[20] # ELEL:spring
  int_Beta[13] <- beta[1] + beta[8] + beta[21] # POSE:coated
  int_Beta[14] <- beta[2] + beta[8] + beta[22] # POFE:coated
  int_Beta[15] <- beta[3] + beta[8] + beta[23] # VUMI:coated
  int_Beta[16] <- beta[4] + beta[8] + beta[24] # ELEL:coated
  int_Beta[17] <- beta[5] + beta[6] + beta[25] # high:fall
  int_Beta[18] <- beta[5] + beta[7] + beta[26] # high:spring
  int_Beta[19] <- beta[5] + beta[8] + beta[27] # high:coated
  int_Beta[20] <- beta[6] + beta[8] + beta[28] # fall:coated
  int_Beta[21] <- beta[7] + beta[8] + beta[29] # spring:coated
  
  # Calculations to monitor, only two-way combinations
  Diff_Beta[1] <- exp(alpha.star + beta[1]) - exp(alpha.star) # POSE
  Diff_Beta[2] <- exp(alpha.star + beta[2]) - exp(alpha.star) # POFE
  Diff_Beta[3] <- exp(alpha.star + beta[3]) - exp(alpha.star) # VUMI
  Diff_Beta[4] <- exp(alpha.star + beta[4]) - exp(alpha.star) # ELEL
  Diff_Beta[5] <- exp(alpha.star + beta[5]) - exp(alpha.star) # high
  Diff_Beta[6] <- exp(alpha.star + beta[6]) - exp(alpha.star) # fall
  Diff_Beta[7] <- exp(alpha.star + beta[7]) - exp(alpha.star) # spring
  Diff_Beta[8] <- exp(alpha.star + beta[8]) - exp(alpha.star) # coated
  
  diff_Beta[1] <- exp(alpha.star + beta[1] + beta[5] + beta[9]) - exp(alpha.star) # POSE:high
  diff_Beta[2] <- exp(alpha.star + beta[2] + beta[5] + beta[10]) - exp(alpha.star) # POFE:high
  diff_Beta[3] <- exp(alpha.star + beta[3] + beta[5] + beta[11]) - exp(alpha.star) # VUMI:high
  diff_Beta[4] <- exp(alpha.star + beta[4] + beta[5] + beta[12]) - exp(alpha.star) # ELEL:high
  diff_Beta[5] <- exp(alpha.star + beta[1] + beta[6] + beta[13]) - exp(alpha.star) # POSE:fall
  diff_Beta[6] <- exp(alpha.star + beta[2] + beta[6] + beta[14]) - exp(alpha.star) # POFE:fall
  diff_Beta[7] <- exp(alpha.star + beta[3] + beta[6] + beta[15]) - exp(alpha.star) # VUMI:fall
  diff_Beta[8] <- exp(alpha.star + beta[4] + beta[6] + beta[16]) - exp(alpha.star) # ELEL:fall
  diff_Beta[9] <- exp(alpha.star + beta[1] + beta[7] + beta[17]) - exp(alpha.star) # POSE:spring
  diff_Beta[10] <- exp(alpha.star + beta[2] + beta[7] + beta[18]) - exp(alpha.star) # POFE:spring
  diff_Beta[11] <- exp(alpha.star + beta[3] + beta[7] + beta[19]) - exp(alpha.star) # VUMI:spring
  diff_Beta[12] <- exp(alpha.star + beta[4] + beta[7] + beta[20]) - exp(alpha.star) # ELEL:spring
  diff_Beta[13] <- exp(alpha.star + beta[1] + beta[8] + beta[21]) - exp(alpha.star) # POSE:coated
  diff_Beta[14] <- exp(alpha.star + beta[2] + beta[8] + beta[22]) - exp(alpha.star) # POFE:coated
  diff_Beta[15] <- exp(alpha.star + beta[3] + beta[8] + beta[23]) - exp(alpha.star) # VUMI:coated
  diff_Beta[16] <- exp(alpha.star + beta[4] + beta[8] + beta[24]) - exp(alpha.star) # ELEL:coated
  diff_Beta[17] <- exp(alpha.star + beta[5] + beta[6] + beta[25]) - exp(alpha.star) # high:fall
  diff_Beta[18] <- exp(alpha.star + beta[5] + beta[7] + beta[26]) - exp(alpha.star) # high:spring
  diff_Beta[19] <- exp(alpha.star + beta[5] + beta[8] + beta[27]) - exp(alpha.star) # high:coated
  diff_Beta[20] <- exp(alpha.star + beta[6] + beta[8] + beta[28]) - exp(alpha.star) # fall:coated
  diff_Beta[21] <- exp(alpha.star + beta[7] + beta[8] + beta[29]) - exp(alpha.star) # spring:coated
  
  # Dsum: posterior predictive loss
  Dsum <- sum(Sqdiff)
}