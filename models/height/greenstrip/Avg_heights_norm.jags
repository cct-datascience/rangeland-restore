# Normal height model
# ANOVA model to predict theta, 2-way interactions included
# theta has random effects only for block (paddock within block is confounded with grazing + block)

model{
  for(i in 1:N){
    # Likelihood
    height[i] ~ dnorm(mu[i], tau)
    
    # replicated data
    height.rep[i] ~ dnorm(mu[i], tau)
    
    # Regression model for mu with random effects
    mu[i] <- alpha + beta[1]*mono[i] + beta[2]*high[i] + beta[3]*coated[i] + 
    beta[4]*fall[i] + beta[5]*spring[i] + beta[6]*mono_high[i] + beta[7]*mono_coated[i] +
    beta[8]*mono_fall[i] + beta[9]*mono_spring[i] + beta[10]*high_coated[i] + 
    beta[11]*high_fall[i] + beta[12]*high_spring[i] + beta[13]*coated_fall[i] +
    beta[14]*coated_spring[i] +
    eps[block[i]]
    
    # Part of Dsum calculation
    Sqdiff[i] <- pow(height.rep[i] - height[i], 2)
  }
  
  # Identifiable intercept, post sweeping random effect means into intercept
  alpha.star <- alpha + mean(eps[])
  
  # Relatively non-informative priors
  
  # random effects of block
  for(b in 1:Nb){ # number of blocks
    eps[b] ~ dnorm(0, tau.eps)
    
    # Identifiable random effects - post-sweeping of mean REs by block
    eps.star[b] <- eps[b] - mean(eps[])
  }
  
  # Block level precisions with folded T priors
  tau.Eps ~ dt(0, Bb, 2)
  sig.eps <- abs(tau.Eps)
  tau.eps <- pow(sig.eps, -2)
  
  # dt parameters (set as data)
  Bb <- 1/(Ab*Ab)
  
  # Coefficient root notes
  alpha ~ dnorm(0, 0.0001)

  for(l in 1:nL){ # Number of treatment offsets
    beta[l] ~ dnorm(0, 0.0001)
  }
  
  # Prior for population level precision
  tau ~ dgamma(0.01, 0.01)
  sig <- pow(tau, -0.5)
  
  # Calculations to monitor, only two-way combinations
  int_Beta[1] <- beta[1] + beta[2] + beta[6] # mono:high
  int_Beta[2] <- beta[1] + beta[3] + beta[7] # mono:coated
  int_Beta[3] <- beta[1] + beta[4] + beta[8] # mono:fall
  int_Beta[4] <- beta[1] + beta[5] + beta[9] # mono:spring
  int_Beta[5] <- beta[2] + beta[3] + beta[10] # high:coated
  int_Beta[6] <- beta[2] + beta[4] + beta[11] # high:fall
  int_Beta[7] <- beta[2] + beta[5] + beta[12] # high:spring
  int_Beta[8] <- beta[3] + beta[4] + beta[13] # coated:fall
  int_Beta[9] <- beta[3] + beta[5] + beta[14] # coated:spring

  
  # Monitor the mean of each group
  m.mix.low.uncoated.ungrazed <- (alpha.star) 
  m.mix.low.uncoated.fall <- (alpha.star + beta[4]) 
  m.mix.low.uncoated.spring <- (alpha.star + beta[5]) 
  
  m.mix.low.coated.ungrazed <- (alpha.star + beta[3]) 
  m.mix.low.coated.fall <- (alpha.star + beta[3] + beta[4] + beta[13]) 
  m.mix.low.coated.spring <- (alpha.star + beta[3] + beta[5] + beta[14]) 
  
  m.mix.high.uncoated.ungrazed <- (alpha.star + beta[2]) 
  m.mix.high.uncoated.fall <- (alpha.star + beta[4] + beta[2] + beta[11]) 
  m.mix.high.uncoated.spring <- (alpha.star + beta[5] + beta[2] + beta[12]) 
  
  m.mix.high.coated.ungrazed <- (alpha.star + beta[3] + beta[2] + beta[10]) 
  m.mix.high.coated.fall <- (alpha.star + beta[3] + beta[4] + beta[13] + beta[2] + beta[11] + beta[10]) 
  m.mix.high.coated.spring <- (alpha.star + beta[3] + beta[5] + beta[14] + beta[2] + beta[12] + beta[10])
  
  m.mono.low.uncoated.ungrazed <- (alpha.star + beta[1]) 
  m.mono.low.uncoated.fall <- (alpha.star + beta[4] + beta[1] + beta[8]) 
  m.mono.low.uncoated.spring <- (alpha.star + beta[5] + beta[1] + beta[9]) 
  
  m.mono.low.coated.ungrazed <- (alpha.star + beta[3] + beta[1] + beta[7]) 
  m.mono.low.coated.fall <- (alpha.star + beta[3] + beta[4] + beta[13] + beta[1] + beta[8] + beta[7]) 
  m.mono.low.coated.spring <- (alpha.star + beta[3] + beta[5] + beta[14] + beta[1] + beta[9] + beta[7]) 
  
  m.mono.high.uncoated.ungrazed <- (alpha.star + beta[2] + beta[1] + beta[6]) 
  m.mono.high.uncoated.fall <- (alpha.star + beta[4] + beta[2] + beta[11] + beta[1] + beta[6] + beta[8]) 
  m.mono.high.uncoated.spring <- (alpha.star + beta[5] + beta[2] + beta[12] + beta[1] + beta[6] + beta[9]) 
  
  m.mono.high.coated.ungrazed <- (alpha.star + beta[3] + beta[2] + beta[10] + beta[1] + beta[6] + beta[7]) 
  m.mono.high.coated.fall <- (alpha.star + beta[3] + beta[4] + beta[13] + beta[2] + beta[11] + beta[10] + beta[1] + beta[6] + beta[7] + beta [8]) 
  m.mono.high.coated.spring <- (alpha.star + beta[3] + beta[5] + beta[14] + beta[2] + beta[12] + beta[10] + beta[1] + beta[6] + beta[7] + beta[9])
  
  # Dsum: posterior predictive loss
  Dsum <- sum(Sqdiff)
}