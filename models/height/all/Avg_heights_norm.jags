# Normal height model
# ANOVA model to predict theta, 2-way interactions included
# theta has random effects only for block (paddock within block is confounded with grazing + block)

model{
  for(i in 1:N){
    # Likelihood
    height[i] ~ dnorm(mu[i], tau)
    
    # replicated data
    height.rep[i] ~ dnorm(mu[i], tau)
    
    # Regression model for mu with random effects
    mu[i] <- alpha + beta[1]*fall[i] + beta[2]*spring[i] +
    beta[3]*herbicide[i] + 
    beta[4]*greenstrip[i] + beta[5]*fall_herbicide[i] + beta[6]*spring_herbicide[i] + 
    beta[7]*fall_greenstrip[i] + beta[8]*spring_greenstrip[i] +
    eps[block[i]]
    
    # Part of Dsum calculation
    Sqdiff[i] <- pow(height.rep[i] - height[i], 2)
  }
  
  # Identifiable intercept, post sweeping random effect means into intercept
  alpha.star <- alpha + mean(eps[])
  
  # Relatively non-informative priors
  
  # random effects of block
  for(b in 1:Nb){ # number of blocks
    eps[b] ~ dnorm(0, tau.eps)
    
    # Identifiable random effects - post-sweeping of mean REs by block
    eps.star[b] <- eps[b] - mean(eps[])
  }
  
  # Block level precisions with folded T priors
  tau.Eps ~ dt(0, Bb, 2)
  sig.eps <- abs(tau.Eps)
  tau.eps <- pow(sig.eps, -2)
  
  # dt parameters (set as data)
  Bb <- 1/(Ab*Ab)
  
  # Coefficient root notes
  alpha ~ dnorm(0, 0.0001)

  for(l in 1:nL){ # Number of treatment offsets
    beta[l] ~ dnorm(0, 0.0001)
  }
  
  # Prior for population level precision
  tau ~ dgamma(0.01, 0.01)
  sig <- pow(tau, -0.5)
  
  # Calculations to monitor, only two-way combinations
  int_Beta[1] <- beta[1] + beta[3] + beta[5] # fall:herbicide
  int_Beta[2] <- beta[2] + beta[3] + beta[6] # spring:herbicide
  int_Beta[3] <- beta[1] + beta[4] + beta[7] # fall:greenstrip
  int_Beta[4] <- beta[2] + beta[4] + beta[8] # spring:greenstrip
  
  
  # Monitor the mean of each group
  m.ungrazed.control <- (alpha.star) 
  m.ungrazed.herbicide <- (alpha.star + beta[3]) 
  m.ungrazed.greenstrip <- (alpha.star + beta[4]) 
  
  m.fall.control <- (alpha.star + beta[1]) 
  m.fall.herbicide <- (alpha.star + beta[1] + beta[3] + beta[5]) 
  m.fall.greenstrip <- (alpha.star + beta[1] + beta[4] + beta[7]) 
    
  m.spring.control <- (alpha.star + beta[2])  
  m.spring.herbicide <- (alpha.star + beta[2] + beta[3] + beta[6]) 
  m.spring.greenstrip <- (alpha.star + beta[2] + beta[4] + beta[8])  
  
  # Dsum: posterior predictive loss
  Dsum <- sum(Sqdiff)
}